# -*- shell-script -*-
# vim: sts=4:sw=4

fpath=(~/.my-config/zsh ~/.zsh/completion $fpath)
localdir=~/.my-config/zsh

# The following lines were added by compinstall

zstyle ':completion:*' completer _expand _complete _match _approximate
zstyle ':completion:*' completions 1
zstyle ':completion:*' glob 1
zstyle ':completion:*' match-original both
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-/\]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' max-errors 2
zstyle ':completion:*' substitute 1
zstyle ':completion::*:git-{name-rev,add,rm}:*' ignore-line true
zstyle ':completion:*' accept-exact '*(N)'
zstyle :compinstall filename '~/.zshrc'

autoload -Uz compinit
compinit
# End of lines added by compinstall
# Lines configured by zsh-newuser-install
HISTFILE=~/.histfile
HISTSIZE=1000
SAVEHIST=1000
setopt appendhistory extendedglob prompt_subst
unsetopt beep
bindkey -e
# End of lines configured by zsh-newuser-install

# Needed by vim completion with deoplete
zmodload zsh/zpty

###
# Collect all the different sources of executables

typeset -U PATH path
path=($HOME/bin $path)
for d in depot_tools .local/bin .yarn/bin .cargo/bin \
    .local/share/coursier/bin; do
    [[ -d $HOME/$d && -z $path[(r)$HOME/$d] ]] && path+=$HOME/$d
done
if [[ -x `which go` ]]; then
    gopath=`go env GOPATH`
    [[ -z $path[(r)$gopath/bin] ]] && path+=$gopath/bin
fi

VISUAL="nvim -f"
EDITOR=$VISUAL
export EDITOR VISUAL

export SHELL=`which zsh`
# For some reason I'm having hard time convincing tmux to offer 256 colours
# otherwise.
if [[ "$TERM" == "screen" ]]; then
    export TERM="screen-256color"
fi

###
# Some OS specific configuration

function update_terminal_cwd() {
    # Identify the directory using a "file:" scheme URL, including
    # the host name to disambiguate local vs. remote paths. This
    # is known as OSC 7 and will be used by Apple Terminal to show
    # the directory, and by iTerm to add some nice semantic markers.

    # Percent-encode the pathname.
    local URL_PATH=''
    {
        # Use LANG=C to process text byte-by-byte.
        local i ch hexch LANG=C
        for ((i = 1; i <= ${#PWD}; ++i)); do
            ch="$PWD[i]"
            if [[ "$ch" =~ [/._~A-Za-z0-9-] ]]; then
                URL_PATH+="$ch"
            else
                hexch=$(printf "%02X" "'$ch")
                URL_PATH+="%$hexch"
            fi
        done
    }

    local PWD_URL="file://$HOST$URL_PATH"
    #echo "$PWD_URL"        # testing
    printf '\e]7;%s\a' "$PWD_URL"

    # Emit OSC 0 to set tab title in iTerm and Gnome terminal. I don't really
    # want to do it under Apple Terminal, because it will already use OSC 7 to
    # derive tab title, unless I'm in SSH.
    if [[ "$TERM_PROGRAM" != "Apple_Terminal" || -n "$SSH_TTY" ]]; then
        # Left-truncated 2 last directories with ~ substitution. The following
        # space substitution is required so that iTerm will not print only the
        # last "word" if path contains spaces.
        printf '\e]0;%s%s\a' \
            "${(%):-${SSH_TTY:+@%m }}" "${${(%):-%20<..<%2~}/ /\ }"
    fi
}

osname=`uname`
if [[ "$osname" = "Darwin" ]]; then
    source $localdir/macrc
elif [[ "$osname" = "Linux" ]]; then
    source $localdir/linuxrc
fi

# Register the function so it is called whenever the working
# directory changes.
autoload add-zsh-hook
add-zsh-hook chpwd update_terminal_cwd

# Tell the terminal about the initial directory.
update_terminal_cwd

###
# See if we can use colors.
eval "$(oh-my-posh init zsh --config ~/.my-config/kali.omp.json)"

# Configure keys
bindkey "^Z"    accept-and-hold
bindkey " "     magic-space  # also do history expansion on space
bindkey "^[[3~" delete-char
bindkey "^[[A"  up-line-or-search
bindkey "^[[B"  down-line-or-search

## Cygwin Console
# Cygwin Console does not distinguish between [Ctrl]+[<|] and [<|]
# respectively [Ctrl]+[|>] and [|>]
if   [ "$TERM" = cygwin ]
then bindkey "\e[1~" beginning-of-line
     bindkey "\e[4~" end-of-line
fi

umask 022

export LESS='RSFX'
export PAGER='less'
export PATH

ext_timefmt='%J %U user %S system %P cpu %*E total
  avg shared (code): %X KB
  avg unshared (data/stack): %D KB
  total (sum): %K KB
  max memory: %M MB
  page faults from disk: %F
  other page faults: %R'

alias cleanbackup="find . -name '.\#*' -or -name '*.moved' -or -name '*~' -exec rm -fr '{}' ';'"

# opam configuration
test -r $HOME/.opam/opam-init/init.zsh && . $HOME/.opam/opam-init/init.zsh > /dev/null 2> /dev/null || true

export FZF_DEFAULT_COMMAND="fd -tf"
export FZF_CTRL_T_COMMAND="fd -L . --min-depth 1 -tf -td -tl"
export FZF_ALT_C_COMMAND="fd -L . --min-depth 1 -td"
[[ -f ~/.fzf.zsh ]] && source ~/.fzf.zsh

export SDKMAN_DIR="$HOME/.sdkman"
[[ -s "$HOME/.sdkman/bin/sdkman-init.sh" ]] && source "$HOME/.sdkman/bin/sdkman-init.sh"
